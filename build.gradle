plugins {
    id 'org.springframework.boot' version '2.2.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id "com.commercehub.gradle.plugin.avro" version "0.9.1"

    id "jacoco"
    id "org.sonarqube" version "3.0"

    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'signing'

    id 'idea'
}

group 'com.bgasparotto'
archivesBaseName = 'spring-kafka-avro-test'
version '1.0.3'
description = "Library with helper methods for testing producers and consumers of Kafka messages with Avro schemas."

repositories {
    mavenCentral()
    maven {
        url 'https://packages.confluent.io/maven/'
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

ext.ossrhUsername = System.getenv("OSSRH_USERNAME")
ext.ossrhPassword = System.getenv("OSSRH_PASSWORD")

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name project.name
                description project.description
                packaging 'jar'
                artifactId 'spring-kafka-avro-test'
                url 'https://github.com/bgasparotto/spring-kafka-avro-test'

                scm {
                    connection 'scm:git:https://github.com/bgasparotto/spring-kafka-avro-test.git'
                    developerConnection 'scm:git:git@github.com/bgasparotto/spring-kafka-avro-test.git'
                    url 'https://github.com/bgasparotto/spring-kafka-avro-test.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'bgasparotto'
                        name 'Bruno Gasparotto'
                        email 'bruno.m.gasparotto@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter'
    api 'org.springframework.kafka:spring-kafka'
    api 'org.springframework.kafka:spring-kafka-test'

    api 'org.apache.avro:avro:1.9.1'
    api 'io.confluent:kafka-avro-serializer:5.3.1'
    api 'io.confluent:kafka-streams-avro-serde:5.3.1'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "bgasparotto_spring-kafka-avro-test"
        property "sonar.organization", "bgasparotto-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}